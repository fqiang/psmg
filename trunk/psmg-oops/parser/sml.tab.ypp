/* (c) 2014 Feng Qiang, Jonathan Hogg and Andreas Grothey, University of Edinburgh
 *
 * This file is part of SML.
 *
 * SML is free software: you can redistribute it and/or modify it under the
 * terms of the GNU Lesser General Public License as published by the Free
 * Software Foundation, using version 3 of the License.
 *
 * SML is distributed in the hope that it will be useful, but WITHOUT ANY
 * WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE. See the GNU Lesser General Public License for more
 * details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with this program. If not, see http://www.gnu.org/licenses/.
 */
%debug

%{
    #define YYERROR_VERBOSE
    #include <stdio.h>
    #include <stdlib.h>
    #include <assert.h>
    #include <typeinfo>
    #include <iostream>
    #include "../model/ParamComp.h"
    #include "../model/VarComp.h"
    #include "../model/ConsComp.h"
    #include "../model/SetComp.h"
    #include "../model/ObjComp.h"
    #include "../model/ModelComp.h" 
    #include "../model/AmplModel.h"
    #include "../model/SyntaxNode.h"
    #include "../model/SyntaxNodeID.h"
    #include "../model/SyntaxNodeValue.h"
    #include "../model/SyntaxNodeIDREF.h"
    #include "../model/SyntaxNodeIDREFM.h"
    #include "../st_model/StochModel.h"

    #include "../util/util.h"
    
    using namespace std;

    void print_index_dummy_vars();
    void record_dummy_var();
    void restore_dummy_var();
 
    void begin_model(const string& name, SyntaxNode *indexing, SyntaxNode* stochsets=NULL);
    void end_model(const string& name);

    extern int yylineno;
    int yylex(void);
    void yyerror(const char *s);

    static AmplModel *current_model = NULL;    /* this is the model currently active */
                                       /* this is the GLOBAL context */

	vector<string> index_dummy_vars;    
	vector<int> dummy_var_size;                                            
	bool isDummyVar(string&);
	                                                
    /* ---------------- stochastic global variables:------------------------ */
    static SyntaxNode *curr_stage_indexset;
    extern FILE *yyin;

	bool isInStochModel();
    void addStochInfo(ModelComp *newmc, SyntaxNode*);
%}

%union
{
  int optype;
  objType otype;
  long *ival;
  double *fval;
  char* string;
  AmplModel* model;
  SyntaxNode *opPtr;
}

%type <opPtr> expr value  expr_list 
%type <opPtr> stochattr_opt 
%type <opPtr> setattributes_opt setattributes setattribute 
%type <opPtr> paramattributes_opt paramattributes paramattribute
%type <opPtr> varattributes_opt varattributes varattribute
%type <opPtr> consattributes consattribute
%type <opPtr> lexpr 
%type <opPtr> setexpression identifier
%type <opPtr> indexing_opt indexing_set 
%type <opPtr> indexing_condition_opt indexing_setexpr_list indexing_setexpr_item /*set_identifier */
%type <opPtr> indexing_dummy indexing_dummy_list
%type <optype> relop reduction_op func_op ubsetop bsetop blogic_op ulogic_op b_compare_op
%type <otype> objdef_type

%token <string> ID 
%token <ival> INT_VAL
%token <fval> FLOAT_VAL 
%token INFINITE COEFF COVER OBJ DEFAULT FROM TO TO_COME MODELTYPE DOTDOT
%token NET_IN NET_OUT DIMEN ORDERED CIRCULAR REVERSED SYMBOLIC ARC
%token INTEGER BINARY CHECK CLOSE DISPLAY DROP INCLUDE PRINT PRINTF QUIT RESET
%token RESTORE SOLVE UPDATE WRITE SHELL MODEL DATA OPTION LET SOLUTION FIX
%token UNFIX END FUNCTION PIPE FORMAT SETOF BY LESS MOD DIV MIN MAX IF THEN
%token ELSE AND OR EXISTS FORALL NOT WITHIN WHILE REPEAT FOR CARD NEXT NEXTW
%token PREV PREVW FIRST LAST MEMBER ORD ORD_ZERO VAR PARAM SET MAXIMIZE MINIMIZE
%token OBJECTIVE SUBJECTTO SUM PROD IN POWER NE LE GE EQ LT GT SIN COS UNION DIFF
%token CROSS INTER SYMDIFF LBRACE RBRACE COMMA SEMICOLON LSBRACKET RSBRACKET
%token COLON LBRACKET RBRACKET DEFINED /* LOGICAL_OR LOGICAL_AND  ELLIPSE */
%token SUFFIX BLOCK
%token USING DETERMINISTIC EXPECTATION STOCHASTIC STAGES
%token ANCESTOR
%token IDREF IDREFM VALUE LIST INDEX DVAR DOT STAGE NODE STRING SUMEXP


%nonassoc ASSIGN
%left OR
%left AND
%left EQ NE
%left LT LE GT GE
%left PLUS MINUS
%left TIMES DIVID
%right SUM
%left POWER
%left '!'
%right DEFINED
%left DOTDOT


%%

statements: /* empty */
          | statements statement 
          ;

statement:  setdef 
         |  paramdef
	     |  vardef
	     |  consdef
	     | 	objdef
 	     |  block
         ;
			         
block:  stochblock
     |  blockblock
     |	stageblock
     ;
         
/* -------------------------------------------------------------------- 
  blockblock
------------------------------------------------------------------------- */
blockblock: BLOCK ID 
			{  
				YAC("blockblock: -- started -- "<<$2);
				record_dummy_var();
			} indexing_opt COLON {   
				begin_model($2,$4);
			} LBRACE statements RBRACE {
               	end_model($2);
               	restore_dummy_var();
            }
          ;
         
/* -------------------------------------------------------------------- 
  stochblock
------------------------------------------------------------------------- */
/* A stochblock is a block definition of the form

     [sblock/stochastic block] name{indexing} using (NODES,ANC,PROB,STAGES):
       statements
     end sblock;

where 
  NODES: set of nodes in the tree
  ANC{NODES}: ancestor for every node
  PROB{NODES}: conditional probability of reaching this node from parent
  STAGES: set of time stages 

-------------------------------------------------------------------- */
stochblock: BLOCK ID
			{
				YAC("stochblock: -- started - "<<$2);
				record_dummy_var();
			} indexing_opt STOCHASTIC USING LBRACKET indexing_setexpr_list RBRACKET COLON 
			{
				begin_model($2, $4, $8);
    	    } LBRACE statements RBRACE 
    	    {
               end_model($2); 
	           restore_dummy_var();
               YAC("vardef: -- end -- "<<$2);
            }
          ;

/* -------------------------------------------------------------------- 
  stageblock
------------------------------------------------------------------------- */

stageblock: STAGES
			{
				assert(isInStochModel() == true);
				record_dummy_var();		
			} indexing_set COLON 
			{    
				curr_stage_indexset = $3;
	        } LBRACE statements RBRACE {
                curr_stage_indexset = NULL;
				restore_dummy_var();            	
            }
          	;

stochattr_opt: /* empty */ {$$ = NULL;}
             |	DETERMINISTIC 
             	{
                	assert(isInStochModel()==true);
                  	$$ = new SyntaxNode(DETERMINISTIC);
             	}
             | 	STAGES
             	{
                	assert(isInStochModel()==true);
             		record_dummy_var();
             	} indexing_set 
             	{
                	restore_dummy_var(); 
                  	$$ = $3;
               	}
             ;


/****************************************************************************/
// Indexing 
// not allowing dummy set declaration
// no recursive indexing set declaration.
// indexing_set : { .... , .... , .... : condition }
/**************************************************************************/     

indexing_opt: /* empty */ {		$$=NULL;	}
            | indexing_set { $$ = $1; }
            ;

indexing_set:	LBRACE indexing_setexpr_list indexing_condition_opt RBRACE 
				{	
					YAC("indexing -- rule matched!");
					SyntaxNode *tmp = NULL;
		      		tmp = new SyntaxNode(LBRACE, new SyntaxNode(COLON, $2, $3));
		           	$$ = tmp;
		           	YAC("indexing -- rule end -- "<<$$);
				}
			;

indexing_condition_opt: /* empty */ { $$=NULL; }
				  		| COLON lexpr
				  		{
				  			$$=$2;
				  		}
				  		;
		

indexing_setexpr_list:  indexing_setexpr_item 
			{  
				$$ = new SyntaxNode(COMMA,$1); 
			}
            |  indexing_setexpr_list COMMA indexing_setexpr_item
            {
                  assert($1->opCode == COMMA);
                  $1->push_back($3);
                  $$ = $1;
			}
            ;

indexing_setexpr_item:	identifier
						{  
							$$ = $1; 
						} 
			            | indexing_dummy_list IN identifier 
			         	{
			              	assert($1->opCode == COMMA);
		              		$$ = new SyntaxNode(IN, $1,$3);
			           	}
			           	| INT_VAL
			           	{
			           		$$ = new SyntaxNodeValue(*$1);
			           	}
			           	| expr DOTDOT expr
			           	{
			           		$$ = new SyntaxNode(DOTDOT, $1, $3);
			           	}
			           	;           

indexing_dummy_list:ID 
					{ 
						SyntaxNodeID* idnode = new SyntaxNodeID($1);
						index_dummy_vars.push_back(idnode->id);
						$$ = new SyntaxNode(COMMA, idnode);
					}
			  		| LBRACKET indexing_dummy RBRACKET 
			  		{ 
			  			$$ = $2;
			  		}
			  		;
		  
indexing_dummy: ID 
				{
					SyntaxNodeID* idnode = new SyntaxNodeID($1);
					index_dummy_vars.push_back(idnode->id);
					$$ = new SyntaxNode(COMMA, idnode);
				}
			   	|indexing_dummy COMMA ID
			   	{
			   		assert($1->opCode == COMMA);
			   		SyntaxNodeID* idnode = new SyntaxNodeID($3);
			   		$1->push_back(idnode);
					index_dummy_vars.push_back(idnode->id);
			   		$$ = $1;
			   	}
			   	;
			   
/****************************************************************************/		
/*  -------------------------------------------------------------------
 set def statement
 -------------------------------------------------------------------   */
setdef:	SET ID
		{
			YAC("setdef: start -- ID=["<<$2<<"]");
			record_dummy_var();
		} indexing_opt stochattr_opt setattributes_opt SEMICOLON
        {
            YAC("indexing_opt=["<<$4<<"]");
            YAC("stochattr_opt=["<<$5<<"]");
            YAC("setattributes_opt=["<<$6<<"]");
            ModelComp *newmc = new SetComp($2, $4, $6,current_model);
            if (isInStochModel()){
            	addStochInfo(newmc, $5);
            }
            current_model->addComp(newmc);
           	
           	restore_dummy_var();
           	YAC("setdef: -- end -- "<<$2);
        }
      ;
      
setattributes_opt:   /* empty */ { $$=NULL; }
                 |	setattributes
                 	{
                 		$$ = $1;
                 	}  
                 ;
                 
setattributes:	setattribute
				{
					$$ = new SyntaxNode(COMMA,$1);
				}
			|	setattributes  setattribute
				{
					assert($1->opCode == COMMA);
					$1->push_back($2);
					$$ = $1;
				}
				;

setattribute: 	DIMEN INT_VAL 
				{
					$$ = new SyntaxNode(DIMEN, new SyntaxNodeValue(*$2));
	           	}
            | 	ASSIGN
            	{
            		record_dummy_var();
            	} setexpression 
	            {
					$$ = new SyntaxNode(ASSIGN, $3);
	            	restore_dummy_var();
				}
			|	WITHIN identifier
				{
					assert($2->opCode == IDREF);
					$$ = new SyntaxNode(WITHIN, $2);
				}
            | 	ORDERED 
            	{ 
            		$$ = new SyntaxNode(ORDERED); 
            	}
            ;      

/*  -------------------------------------------------------------------
 param def statement
 -------------------------------------------------------------------   */
paramdef:  	PARAM ID
			{
				YAC("paramdef: -- start -- "<<$2);
				record_dummy_var();
			} indexing_opt stochattr_opt paramattributes_opt SEMICOLON {
			   	ModelComp *newmc = new ParamComp($2,$4,$6,current_model);
			   	if (isInStochModel())
			   	{
			      	addStochInfo(newmc, $5);
			   	}
			   	current_model->addComp(newmc);
               	
               	restore_dummy_var();
               	YAC("paramdef: -- end -- "<<$2);
            }
        ;
        
paramattributes_opt: /* empty */ { $$ = NULL; }
                   | paramattributes
                   	 {
                   	 	$$ =$1;
                   	 }
                   	 
                   ;

paramattributes: paramattribute
				{
					$$ = new SyntaxNode(COMMA,$1);
				}
				| paramattributes paramattribute
				{
					assert($1->opCode == COMMA);
					$1->push_back($2);
					$$ = $1;
				}
				;
                   
paramattribute:   BINARY {$$ = new SyntaxNode(BINARY);}
              |   INTEGER {$$ = new SyntaxNode(INTEGER);}
              |   SYMBOLIC {$$ = new SyntaxNode(SYMBOLIC);}
              |   relop expr {$$ = new SyntaxNode($1, $2);}
              |   DEFAULT expr {$$ = new SyntaxNode(DEFAULT, $2);}
              |   DEFINED expr {$$ = new SyntaxNode(DEFINED, $2);}
              ;
              
relop:   LE {$$=LE;}
     |   GE {$$=GE;}
     |   LT {$$=LT;}
     |   GT {$$=GT;}
     |   EQ {$$=EQ;}
     |   NE {$$=NE;}
     ;
     	   
/*  -------------------------------------------------------------------
 var def statement
 -------------------------------------------------------------------   */
vardef: VAR ID 
		{
			YAC("vardef: -- start -- "<<$2);
			record_dummy_var();
		}indexing_opt stochattr_opt varattributes_opt SEMICOLON {
            ModelComp *newmc = new VarComp($2, $4, $6,current_model);
            if (isInStochModel()){
            	addStochInfo(newmc, $5);
            }
            current_model->addComp(newmc);
          	restore_dummy_var();
          	YAC("vardef: -- end -- "<<$2);
        }
      ;
     
varattributes_opt:   /* empty */ {$$=NULL;}
                 |   varattributes {$$ = $1;}
                 ;


varattributes: varattribute 
			   {
					$$ = new SyntaxNode(COMMA,$1); 
               }
             | varattributes COMMA varattribute 
               {
               		assert($1->opCode == COMMA);
             		$1->push_back($3);
             		$$ = $1;
               }
             ;

varattribute:  BINARY {$$ = new SyntaxNode(BINARY);}
            |  INTEGER {$$ = new SyntaxNode(INTEGER);}
           	|  LT expr  { $$ = new SyntaxNode(LT, $2); }
			|  LE expr  { $$ = new SyntaxNode(LE, $2); }
			|  GT expr  { $$ = new SyntaxNode(GT, $2); }
			|  GE expr  { $$ = new SyntaxNode(GE, $2); }
            |  DEFAULT expr {$$ = new SyntaxNode(DEFAULT, $2);}
            |  SUFFIX ID expr {$$ = new SyntaxNode(SUFFIX, new SyntaxNodeID($2), $3);}
            ;
     
/*  -------------------------------------------------------------------
 consdef def statement
 -------------------------------------------------------------------   */     
consdef:	SUBJECTTO ID
			{
				YAC("consdef: -- start -- "<<$2);
				record_dummy_var();
			} indexing_opt stochattr_opt COLON consattributes SEMICOLON
			{
				YAC("consdef: -- indexing -- "<<$4);
				ModelComp *newmc = new ConsComp($2, $4, $7,current_model);
	            if (isInStochModel()){
					addStochInfo(newmc, $5);
	            }
	            current_model->addComp(newmc);
	            
	            restore_dummy_var();
	            YAC("consdef: -- end -- "<<$2);
			}
			;
			
consattributes: consattribute 
			{
				$$ = new SyntaxNode(COMMA, $1);
			}
			|	consattributes COMMA consattribute
			{
				assert($1->opCode == COMMA);
         		$1->push_back($3);
         		$$ = $1;
			}
			;
			
consattribute: expr ASSIGN expr  
			{ 
				assert($1!=NULL);
				assert($3!=NULL);
				$$ = new SyntaxNode(ASSIGN, $1, $3);  
			}
			|  LT expr  { $$ = new SyntaxNode(LT, $2); }
			|  LE expr  { $$ = new SyntaxNode(LE, $2); }
			|  GT expr  { $$ = new SyntaxNode(GT, $2); }
			|  GE expr  { $$ = new SyntaxNode(GE, $2); }

/*-------------------------------------------------------------------------
objdef def statement
--------------------------------------------------------------------------*/
objdef: objdef_type ID stochattr_opt COLON expr SEMICOLON
		{
			YAC("objdef: -- "<<$2);
			ModelComp *newmc = new ObjComp($2, $1, $5,current_model);
            if (isInStochModel()){
	           addStochInfo(newmc, $3);
            }
            
            current_model->addComp(newmc);
            YAC("objdef: -- end -- "<<$2);
		}
		;
		
objdef_type:	MAXIMIZE { $$ = TMAX; }  
  			| 	MINIMIZE { $$ = TMIN; }
  			;
  			
/*-------------------------------------------------------------------------*/
			   
/* this is anything that evaluates to a set */
setexpression: 	indexing_set 
				{
					YAC("setexpression: indexing -- "<<$1);
					$$= $1;
				}
				| expr DOTDOT expr
	           	{
	           		$$ = new SyntaxNode(DOTDOT, $1, $3);
	           	} 
			 	| identifier 
			 	{
		     		YAC("setexpression: identifier -- rule matched -- "<<$1);
		     		assert($1->opCode == IDREF);
		     		$$ = $1;
             	}
             	| SETOF 
             	{ 
	         		YAC("setexpression - setof -- start ");
	         		record_dummy_var();
	         	} indexing_set identifier {
	         		restore_dummy_var();
	         		$$ = new SyntaxNode(SETOF,$3,$4);
	            }
             	| setexpression bsetop setexpression {
					$$ = new SyntaxNode($2, $1, $3);
               	}
             	| setexpression ubsetop setexpression {
                  	$$ = new SyntaxNode($2, $1, $3);
               	}
             	;


bsetop:  DIFF     { $$ = DIFF; }
      |  SYMDIFF  { $$ = SYMDIFF; }
      |  CROSS    { $$ = CROSS; }
      ;

ubsetop: UNION    { $$ = UNION; }
      |  INTER    { $$ = INTER; }
      ;
      
/*------------------------------------------------------------------------------------ */

//Logical expression
/*------------------------------------------------------------------------------------ */
lexpr:  identifier IN identifier {
			YAC("lexpr: identifier IN identifier - rule matched! "<<$1<<" "<<$3);
			assert($3->opCode == IDREF);
			$$ = new SyntaxNode(IN,$1,$3);
		}
     |  LBRACKET lexpr RBRACKET {
            $$ = $2; 
		}
     |  lexpr blogic_op lexpr 
     	{
     		$$ = new SyntaxNode($2,$1,$3);
     	}
     |  ulogic_op lexpr
     	{
     		$$ = new SyntaxNode($1,$2);
     	}
     |  expr b_compare_op expr 
     	{
     		$$ = new SyntaxNode($2, $1, $3);
     	}
     ;
     
b_compare_op: NE { $$ = NE; }
			| LE { $$ = LE; }
			| GE { $$ = GE; }
			| LT { $$ = LT; }
			| GT { $$ = GT; }
			| EQ { $$ = EQ; }
			;
			
blogic_op: AND { $$ = AND; }
		| OR  {  $$ = OR; }
		;
ulogic_op: NOT  {  $$ = NOT; }
		;
		
/*------------------------------------------------------------------------------------ */
      

/* Identifier is any quotation of a predefined object:
    RoutComm[j].Flow[k-3*j, k]#
   An iditem is simply a tree of SyntaxNodes that serve as an identifier.
   An 'identifer' is a SyntaxNode of type IDREF or IDREFM.
*/
identifier: ID 
			{
			   	YAC("identifier: ID  -- "<<$1);
			   	string key = $1;
			   	if(isDummyVar(key))
			   	{
					$$ = new SyntaxNodeID($1);
			   	}
			   	else
			   	{
			   		// a better implementation will be findModelComp below and above current model according to 
			   		// different cases: 1. a dot notation. ie. identifier.identifier[index] or 2. just a simple identifier.
			   		// for case 1: first identifier should reference to a model below current model
			   		// for case 2: identifier should be anything above or at current model.
			   		ModelComp* 	ref = current_model->findModelComp(key); 
			   		if(ref!=NULL && ref->type == TMODEL){
			   		   	$$ = new SyntaxNodeIDREFM(new SyntaxNodeID($1),static_cast<AmplModel*>(ref));
				   	}
				   	else if(ref!=NULL && (ref->type == TPARAM || ref->type == TVAR || ref->type == TSET))
				   	{
				   	   	$$ = new SyntaxNodeIDREF(new SyntaxNodeID($1),ref);
				   	}
				   	else
				   	{
				   		YAC("error !"<<$1<<" undefined!");
				   		cerr<<"model error: "<<$1<<" undefined!"<<endl;
				   		assert(false);
				   	}
			   	}
            }
          | ANCESTOR LBRACKET INT_VAL RBRACKET DOT identifier
          	{
				assert(isInStochModel()==true);
	            assert($6->opCode == IDREF);
				$$ = new SyntaxNode(ANCESTOR, new SyntaxNodeValue(*$3), $6);
          	}
          | identifier LSBRACKET expr_list RSBRACKET
          	{
				assert($1->opCode == IDREF || $1->opCode == IDREFM);
          		$1->push_back($3);
          		$$ = $1;
          	}    
          | identifier DOT
          	{
               	$<model>$ = current_model;
          		assert($1->opCode == IDREFM);
          		current_model = static_cast<SyntaxNodeIDREFM*>($1)->ref;
			}
			identifier
			{ 
				$$ = new SyntaxNode(DOT, $1, $4); 
				current_model = $<model>3;
			}
          ;

/*------------------------------------------------------------------------------------ */

expr_list:	expr 
			{
				YAC("expr_list: expr rule matched! "<<$1);
               	$$ = new SyntaxNode(COMMA,$1);
            }
         |  expr_list COMMA expr 
         	{ 
         		YAC("expr_list: expr_list COMMA expr -- "<<$1);
         		YAC("expr_list: expr_list COMMA expr -- "<<$3);
               	assert($1->opCode == COMMA);
               	$$ = $1->push_back($3);
            }
         ;

expr: value 
	{ 
		YAC("expr: value rule matched!" <<$1);
		$$ = $1;
	}
	| identifier
	{
		$$ = $1;
	}
    | LBRACKET expr RBRACKET { $$ = $2; }
    | MINUS expr { $$ = new SyntaxNode(MINUS, $2); }
    | expr PLUS expr { $$ = new SyntaxNode(PLUS, $1, $3); }
    | expr MINUS expr { $$ = new SyntaxNode(MINUS, $1, $3); }
    | expr TIMES expr { $$ = new SyntaxNode(TIMES, $1, $3); }
    | expr DIVID expr { $$ = new SyntaxNode(DIVID, $1, $3); }
    | expr POWER expr { $$ = new SyntaxNode(POWER, $1, $3); }
    /* | expr ASSIGN expr { $$ = new SyntaxNode(ASSIGN, $1, $3); } */
    | IF lexpr THEN expr { $$ = new SyntaxNode(IF, $2, $4); }
    | IF lexpr THEN expr ELSE expr { $$ = new SyntaxNode(IF, $2, $4, $6); }
    | EXPECTATION LBRACKET expr RBRACKET 
      { 
      	$$ = new SyntaxNode(EXPECTATION, $3);
      }
    | func_op LBRACKET expr RBRACKET 
      { 
      	$$ = new SyntaxNode($1, $3);
      }
    | reduction_op
	  {	
	  	record_dummy_var();
	  } indexing_set LBRACKET expr RBRACKET 
      {  
		restore_dummy_var();
      	$$ = new SyntaxNode($1, $3, $5);
      }
    ;

func_op : ORD  { $$=ORD;   }
		| CARD { $$=CARD;  }
		| SIN  { $$ = SIN; }
		| COS  { $$ = COS; }
		;

reduction_op:  SUM { $$=SUM; }
            |  MAX { $$=MAX; }
            |  MIN { $$=MIN; }
            |  PROD { $$=PROD; }
            ;

value:   INT_VAL {
            $$=new SyntaxNodeValue(*$1);
         }
     |   FLOAT_VAL { 
            $$=new SyntaxNodeValue(*$1);
         }
     |   INFINITE { 
            $$ = new SyntaxNode(INFINITE);
         }
     ;

%%

void yyerror(const char *s) {
   cerr << "MODEL: " << s << " on line " << yylineno << "\n";
}


/* ----------------------------------------------------------------------------
yywrap
---------------------------------------------------------------------------- */
/* not sure if this is correct, found this somewhere on the internet
   should open the data file and somehow tell the parser to carry on 
   reading in "data mode"
 */
int yywrap(void) {
   return 1;
}

/* ----------------------------------------------------------------------------
begin_model
---------------------------------------------------------------------------- */
void begin_model(const string& name, SyntaxNode *indexing, SyntaxNode* stochsets) 
{
	YAC("begin_model  ["<< name <<"] indexing["<<indexing<<"] -- in current model["<<current_model->name<<"]");
	AmplModel* new_mod = NULL;
	if(stochsets!=NULL)
	{
		assert(indexing==NULL); // stochmodel has indexing=NULL
		assert(stochsets!=NULL && stochsets->nchild()==4);
		YAC(" model ["<<name<<"] is stochastic block!" );
		new_mod = new StochModel(name, stochsets, current_model);
	}
	else
	{
		new_mod = new AmplModel(name,indexing,current_model);
	}
	current_model->addComp(new_mod);  
	current_model = new_mod;
	YAC("     current model now "<<current_model->name);
}

/* ----------------------------------------------------------------------------
end_model
---------------------------------------------------------------------------- */
void end_model(const string& name) 
{
	YAC("end_model -- ["<<current_model->name<<"] -- isStoch["<<isInStochModel()<<"]");
	// Check end block name matches block name
	if (name.compare(current_model->name)!=0) {
		cerr << "end model '" << name << "' encountered in model '" << current_model->name << "'" << endl;
		exit(1);
	}
	current_model = current_model->parent;
	assert(current_model!=NULL);
}


/* ---------------------------------------------------------------------------
Stochastic model helper functions
---------------------------------------------------------------------------- */
void addStochInfo(ModelComp *newmc, SyntaxNode *stochopt) {
	assert(isInStochModel() == true);
	if(curr_stage_indexset != NULL)
	{
		if(stochopt!=NULL && stochopt->opCode == DETERMINISTIC) {
			assert(newmc->type == TVAR);   //DETERMINISTIC only meaningful for var comp
			static_cast<VarComp*>(newmc)->isDet = true;
		}
		else if(stochopt!=NULL)
		{
			cerr<<"ambiguous stage set declaration in side a stageblock!"<<endl;
	 		assert(false);
	 		exit(1);
		}
		newmc->stage = curr_stage_indexset;
	}
	else {
		newmc->stage = stochopt;
	}
}
bool isInStochModel()
{
	if(typeid(StochModel) == typeid(*current_model))
	{
		return true;
	}
	return false;
}

/* ------------------------------------------------------------------------
add_indexing/rem_indexing
-------------------------------------------------------------------------- */
void print_index_dummy_vars()
{
	YAC("print_index_dummy_vars -- ");
	for(int i=0;i<index_dummy_vars.size();i++)
	{
		YAC("["<<i<<"] "<<index_dummy_vars[i]);
	}	
}

void record_dummy_var()
{
	YAC("record_dummy_var -- size["<<index_dummy_vars.size()<<"]");
	dummy_var_size.push_back(index_dummy_vars.size());
}

void restore_dummy_var()
{
	YAC("restore_dummy_var -- size["<<index_dummy_vars.size()<<"]");
	print_index_dummy_vars();
	int pre_size = dummy_var_size.back();
	assert(pre_size <= index_dummy_vars.size());
	for(int i=index_dummy_vars.size()-1;i>=pre_size;i--)
	{
		YAC("remove - "<<index_dummy_vars[i]);
	}
	dummy_var_size.pop_back();
	index_dummy_vars.resize(pre_size);
	print_index_dummy_vars();
}

bool isDummyVar(string& v)
{
	for(vector<string>::iterator it=index_dummy_vars.begin();it!=index_dummy_vars.end();it++)
	{
		if(v.compare(*it)==0)
		{
			return true;
		}
	}
	return false;
}

//yyin opened,and readable
int parse_model() 
{
	int errcode = 0;
	yyin = fopen(GV(modelfilename).c_str(),"r");
	assert(errcode==0);
	string name = "root";
	//creating the root level - as always the case 
   	AmplModel::root = new AmplModel(name,NULL,NULL);
   	current_model = AmplModel::root;
   	current_model->level = 0;
   	
   	//stochastic model related initialization
   	curr_stage_indexset = NULL;

	YAC("starting model parser.... ");
   	errcode = yyparse();
   	assert(errcode==0);
   	YAC("model parser finished !");
   	
   	fclose(yyin);
   	assert(errcode==0);
	return errcode;
}
