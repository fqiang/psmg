/* (c) 2008,2009 Jonathan Hogg and Andreas Grothey, University of Edinburgh
 *
 * This file is part of SML.
 *
 * SML is free software: you can redistribute it and/or modify it under the
 * terms of the GNU Lesser General Public License as published by the Free
 * Software Foundation, using version 3 of the License.
 *
 * SML is distributed in the hope that it will be useful, but WITHOUT ANY
 * WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE. See the GNU Lesser General Public License for more
 * details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with this program. If not, see http://www.gnu.org/licenses/.
 */

%debug
%{
 	#include "../util/global_util_functions.h"
   	#include "../sml/GlobalVariables.h"
   	#include "../context/Set.h"
	#include "../context/ModelContext.h"
   	#include "../context/Param.h"
   	#include "../model/AmplModel.h"
   	#include "../model/ModelComp.h"
   	#include <cassert>
   	#include <cstdlib>
   	#include <iostream>
   	#include <sstream>
   	#include <errno.h>

   	#define YYERROR_VERBOSE

   	using namespace std;

   	int datalex(void);
   	static void dataerror(const char *s);
   	extern FILE *datain;
   	extern int datalineno;
   	ModelContext* rootContext;
 
%}

%union
{
   long ival;
   double fval;
   char* string;
}

%type <string> set_name 
%type <string> set_value
%type <string> member_pairs member_pair
%type <string> member_items 
%type <string> member


%type <string> param_name  param_names
%type <string> param_specs param_specs_item


%token <string> ID 
%token <string> INT
%token <string> FLOAT 

%token DATA
%token SET PARAM TR
%token DEFINE
%token PLUS MINUS DOT SEMICOLON COLON 
%token LBRACE RBRACE LSBRACKET RSBRACKET LBRACKET RBRACKET
%token COMMA


%nonassoc SEMICOLON

%%

statements: /* empty */
          	| statements statement 
          	;

statement:  set_def
			| param_def
			| DATA SEMICOLON {
				YAC_DATA_LOG("data rule matched");
			}
         	;

set_def:	SET set_name DEFINE member SEMICOLON {
				YAC_DATA_LOG("set define start - FOR === ["<<$2<<"]");
				YAC_DATA_LOG("   ===== VALUE ====");
				YAC_DATA_LOG($4);
				
				string id($2);
				ModelComp* setComp = AmplModel::root->findModelComp(id,TSET);
				Set* setValue = new Set(setComp->setDim,setComp->id);
				setValue->fillSetValues($4);
				setComp->setCard = setValue->card;
				rootContext->addCompValueMap(setComp,setValue);
        	}
			;            

set_name:	ID { 
				$$ = $1;
			}
			;

member:		  member_items {
				//YAC_DATA_LOG("member_items["<<$1<<"]"); 
				$$ = $1; 
			}
			| member_pairs { 
				//YAC_DATA_LOG("member_pairs["<<$1<<"]");
				$$ = $1;
			}
			;

member_items: set_value {
				//YAC_DATA_LOG("last set value ["<<$1<<"]");
				$$ = $1;
			}
			| member_items set_value {
				ostringstream oss(ostringstream::out);
				oss<<$1<<','<<$2;
				free($1);
				free($2);
				$$ = strdup(oss.str().c_str());
			}
			;

member_pairs: LBRACKET member_pair RBRACKET {
				//YAC_DATA_LOG("last set value tuple ["<<$2<<"]");
				ostringstream oss(ostringstream::out);
				oss<<'('<<$2<<')';
				free($2);
				$$ = strdup(oss.str().c_str());
			}
			| member_pairs LBRACKET member_pair RBRACKET {
				//YAC_DATA_LOG("a set value tuple ["<<$3<<"]");
				ostringstream oss(ostringstream::out);
				oss<<$1<<'('<<$3<<')';
				free($1);
				free($3);
				$$ = strdup(oss.str().c_str());
			}
			;
			
member_pair: set_value  { 
				$$ = $1;
			}
			| member_pair COMMA set_value {
				ostringstream oss(ostringstream::out);
				oss<<$1<<','<<$3;
				free($1);
				free($3);
				$$ = strdup(oss.str().c_str());
			}
			;

param_def:	PARAM param_name DEFINE param_specs SEMICOLON { 
				YAC_DATA_LOG("single param define start -FOR === ["<<$2<<"]");
				YAC_DATA_LOG("   ===== VALUE ====");
				YAC_DATA_LOG($4);
				string id($2);
				ModelComp* paramComp = AmplModel::root->findModelComp(id,TPARAM);
				Param* param_ = new Param(paramComp->getNumParamIndicies(),paramComp->id);
                assert(paramComp->getNumParamIndicies() == 0);
				double pvalue = atof($4);
				param_->addParamValue("",pvalue);
                rootContext->addCompValueMap(paramComp,param_);
			}
			| PARAM COLON param_names DEFINE param_specs SEMICOLON {
				YAC_DATA_LOG("multiple params define start -FOR === ["<<$3<<"]");
				YAC_DATA_LOG("   ===== VALUE ====");
				YAC_DATA_LOG($5);
				
				char* tok = strtok($3,",");
				vector<ModelComp*> paramModels;
				vector<Param*> params;
				int numIndicies = 0;
				int numIndicies_init = -1;
				while(tok!=NULL)
				{
					string id(tok);
					ModelComp* paramComp = AmplModel::root->findModelComp(id,TPARAM);
					paramModels.push_back(paramComp);
					Param* paramValue = new Param(paramComp->getNumParamIndicies(),paramComp->id);
					params.push_back(paramValue);
					rootContext->addCompValueMap(paramComp,paramValue);
					
					numIndicies = paramComp->getNumParamIndicies();
					if(numIndicies_init == -1) numIndicies_init = numIndicies;
					assert(numIndicies_init==numIndicies);
					
					tok = strtok(NULL,",");
				}
				
				ostringstream oss(ostringstream::out);
                int curr_index = 0;
				tok = strtok($5,",");
				while(tok!=NULL)
				{
					oss<<tok;
                    ++curr_index;
					if(curr_index == numIndicies)
                    {
                            for(vector<Param*>::iterator i=params.begin();i!=params.end();i++)
                            {
                                    tok=strtok(NULL,",");
                                    double pvalue = atof(tok);
                                    (*i)->addParamValue(oss.str(),pvalue);
                            }
                            oss.str("");
                            curr_index = 0;
                    }
                    tok = strtok(NULL,",");
				}
			}
			;

param_names: param_name {
				$$ = $1;
			}
			|	param_names param_name {
				ostringstream oss(ostringstream::out);
				oss<<$1<<','<<$2;  //each param_name(set_value) is on newline
				free($1);
				free($2);
				$$ = strdup(oss.str().c_str());
			}
			;

param_specs: param_specs_item{
				$$ = $1;
			}
			| param_specs param_specs_item {
				ostringstream oss(ostringstream::out);
				oss<<$1<<','<<$2;  //each on newline
				free($1);
				free($2);
				$$ = strdup(oss.str().c_str());
			}
			;
			
param_specs_item: ID {
				$$ = $1;
			}
			| INT {
				$$ = $1;
			}
			| FLOAT {
				$$ = $1;
			}
			;

param_name:	ID {
				$$ = $1;
			}
			;	

set_value: ID { 
				$$ = $1;
			}
			| INT {
				$$ = $1;
			}
			;

%%

void dataerror(const char *s) {
  cerr << "INPUT:[" << s << "] on line " << datalineno << "\n";
  exit(1);
}

void parse_data(ModelContext* context) {
       
	YAC_DATA_LOG("===============================================================");
	YAC_DATA_LOG(" Start parsing data file: " << GlobalVariables::datafilename);
	YAC_DATA_LOG("===============================================================");
	rootContext = context;
  	datain = fopen(GlobalVariables::datafilename.c_str(), "r");
  	if (datain==NULL){
    	printf("ERROR: Data file '%s' error:%s \n",GlobalVariables::datafilename.c_str(),strerror(errno));
    	exit(1);
  	}
          
  	dataparse();
  	YAC_DATA_LOG("===============================================================");
  	YAC_DATA_LOG(" Finished parsing data file");
  	YAC_DATA_LOG("===============================================================");
	fclose(datain);
}

static void print_token_value (FILE *file, int type, YYSTYPE value)
{
	fprintf (file, "%s", value.string);
}


//main(int argc,char** argv )
//{
// 	++argv, --argc;  /* skip over program name */
// 	if ( argc > 0 )
// 	{
//	   	datain = fopen( argv[0], "r" );
//  }
// 	else
// 	{
//     	datain = stdin;
//  }
//	
//	while(dataparse());
//}

